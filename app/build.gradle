plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'checkstyle'
    id 'io.ebean' version '12.15.1'
}

compileJava {
    options.release = 17
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'hexlet.code.App'
}

group 'hexlet.code'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.javalin:javalin:4.3.0'

    implementation 'org.slf4j:slf4j-simple:1.7.36'

    implementation 'org.thymeleaf:thymeleaf:3.0.12.RELEASE'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.0.0'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'

    implementation 'org.webjars:bootstrap:5.2.2'

    implementation 'org.jsoup:jsoup:1.15.3'

    implementation 'com.konghq:unirest-java:3.13.11'

    implementation 'com.h2database:h2:2.1.210'
    implementation 'org.postgresql:postgresql:42.3.3'

    implementation 'io.ebean:ebean:12.15.1'
    implementation 'io.ebean:ebean-ddl-generator:12.15.1'
    implementation 'io.ebean:ebean-querybean:12.15.1'
    implementation 'io.ebean:ebean-migration:12.15.1'
    implementation 'io.ebean:ebean-annotation:7.7'
    annotationProcessor 'io.ebean:querybean-generator:12.15.1'

    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'io.ebean:ebean-test:12.15.1'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

jacoco {
    toolVersion = "0.8.8"
}

testlogger {
    showStandardStreams true
}

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.MigrationGenerator'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean
